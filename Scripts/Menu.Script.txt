#RequireContext CManiaAppTitle

#Include "TextLib" as TextLib

#Include "Libs/BigBang1112/Layers.Script.txt" as Layers
#Include "Libs/BigBang1112/Task.Script.txt" as Task
#Include "Libs/BigBang1112/Manialink.Script.txt" as Manialink
#Include "Libs/BigBang1112/Time.Script.txt" as Time
#Include "Libs/BigBang1112/Dictionary.Script.txt" as Dictionary

Void CalculateMap(Text _FileName, Text _MapType) {
	TitleControl.EditMap(_FileName, "Shadow.Script.txt", """<settings>
		<setting name="S_Enable" type="boolean" value="True"/>
		<setting name="S_MapType" type="text" value="{{{_MapType}}}"/>
	</settings>""");
}

main() {
	Dictionary::SetTranslationFolder("Media/Translations");
	Dictionary::SetTranslationLanguage(LocalUser.Language);
	
	Layers::Create("UI", Manialink::FromFile("Media/Manialinks/UI", True), True);
	Layers::Create("QNA", Manialink::Construct("", ""));
	Layers::Create("QNA_BACK", Manialink::FromFile("Media/Manialinks/QNA_Back", True));
	
	declare MapType = "";

	while(True) {
		foreach(Event,PendingEvents) {
			if(Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
				switch(Event.CustomEventType) {
					case "ComputeAll": {
						declare Maps = Event.CustomEventData[0];
						MapType = Event.CustomEventData[1];
					
						if(TextLib::EndsWith(".Gbx",Maps)) {
							if(TitleControl.IsReady)
								CalculateMap(Event.CustomEventData[0], MapType);
						}
						else
							Task::Async_DataFileMgr("MAPS", DataFileMgr.Map_GetFilteredGameList(2, Maps, True));
					}
					case "QNA": {
						Layers::Hide("UI");
						Layers::Show("QNA");
						Layers::Show("QNA_BACK");
					}
					case "QNA_BACK": {
						Layers::Show("UI");
						Layers::Hide("QNA");
						Layers::Hide("QNA_BACK");
					}
				}
			}
		}
		
		if(Task::IsCompleted("MAPS")) {
			declare Task = Task::ToMapList(Task::Get("MAPS"));
			if(Task::IsSuccessful("MAPS")) {
				Layers::SendEvent("UI","CHANGE_MODE",["RUNNING"]);
				
				declare Terminate = False;
				foreach(MapInfo,Task.MapInfos) {
					if(!Terminate) {
						wait(TitleControl.IsReady);
						CalculateMap(MapInfo.FileName, MapType);
						wait(TitleControl.IsReady);
							
						Time::Stamp_Create("TERMINATE",2000);
						while(!Terminate) {
							foreach(Event,PendingEvents)
								if(Event.Type == CManiaAppEvent::EType::LayerCustomEvent)
									if(Event.CustomEventType == "TERMINATE")
										Terminate = True;
							if(Time::Stamp_Reached("TERMINATE")) break;
							yield;
						}
						log("reached");
					}
				}
				
				Layers::SendEvent("UI","CHANGE_MODE",["NORMAL"]);
			}
			else {
			
			}
			Task::Destroy_DataFileMgr("MAPS");
		}
		
		yield;
	}
}