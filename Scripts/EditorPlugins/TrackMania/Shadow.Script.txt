#RequireContext CMapEditorPlugin

#Include "TextLib" as TextLib

#Setting S_Enable False
#Setting S_MapType ""

main() {
	if(S_Enable) {
		HideEditorInterface = True;
		wait(IsEditorReadyForRequest);

		if(TextLib::Find(".", MapName, False, False)
		|| TextLib::Find("\\", MapName, False, False)) {
			Dialog_Message("Map name contains invalid characters. Skipping in 3 seconds...");
			sleep(3000);
			QuickQuit();
		}
		else {
			if(CurrentShadowsQuality == ::ShadowsQuality::NotComputed
			|| CurrentShadowsQuality == ::ShadowsQuality::VeryFast
			|| CurrentShadowsQuality == ::ShadowsQuality::Fast) {
				wait(IsEditorReadyForRequest);
				ComputeShadows(::ShadowsQuality::Default);
				
				wait(IsEditorReadyForRequest);
				if(S_MapType != "")
					declare SuccessfulMapTypeIGuess = SetMapType("TrackMania\\"^S_MapType);
				
				wait(IsEditorReadyForRequest);
				declare Folders = TextLib::Split("\\",MapFileName);
				declare Removed = Folders.removekey(Folders.count-1);
				SaveMap(MapName, TextLib::Join("\\",Folders)^"\\");
	
				wait(IsEditorReadyForRequest);
				QuickQuit();
			}
			else {
				ComputeShadows(::ShadowsQuality::Default); // This will put the lightmap file higher in date
				wait(IsEditorReadyForRequest);
				Dialog_Message("Map is already calculated in your cache. The lightmaps won't be possibly stored in a map after saving, so the map save will be skipped. Follow the steps to recalculate the shadows properly.\nFind and delete the lightmap file (.Bump.LightMap.zip) from ProgramData/ManiaPlanet/Cache (it will be one of the newest) and try this map again.");
				sleep(18000);
				QuickQuit();
			}
		}
	}
}