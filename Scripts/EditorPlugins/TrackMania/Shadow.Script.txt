#RequireContext CMapEditorPlugin

#Include "TextLib" as TextLib

#Include "Libs/BigBang1112/Dictionary.Script.txt" as Dictionary

#Setting S_Enable False
#Setting S_MapType ""
#Setting S_DontSaveAlreadyCalculated True

main() {
	Dictionary::SetTranslationFolder("Media/Translations");
	Dictionary::SetTranslationLanguage(LocalUser.Language);

	if(S_Enable) {
		HideEditorInterface = True;
		wait(IsEditorReadyForRequest);

		if(TextLib::Find(".", MapName, False, False)
		|| TextLib::Find("\\", MapName, False, False)) {
			Dialog_Message(Dictionary::Get("INVALID_CHARACTERS"));
			sleep(3000);
			QuickQuit();
		}
		else {
			declare AlreadyCalculated = (CurrentShadowsQuality == ::ShadowsQuality::Default
										|| CurrentShadowsQuality == ::ShadowsQuality::High
										|| CurrentShadowsQuality == ::ShadowsQuality::Ultra);
			wait(IsEditorReadyForRequest);
			ComputeShadows(::ShadowsQuality::Default);
			
			wait(IsEditorReadyForRequest);
			if(S_MapType != "")
				declare SuccessfulMapTypeIGuess = SetMapType("TrackMania\\"^S_MapType);
			
			if(!AlreadyCalculated || !S_DontSaveAlreadyCalculated) {
				wait(IsEditorReadyForRequest);
				declare Folders = TextLib::Split("\\",MapFileName);
				declare Removed = Folders.removekey(Folders.count-1);
				SaveMap(MapName, TextLib::Join("\\",Folders)^"\\");
			}

			wait(IsEditorReadyForRequest);
			QuickQuit();
		}
	}
}